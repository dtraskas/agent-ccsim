@echo off
 
REM "<copyright>"
REM " "
REM " Copyright 2001-2007 BBNT Solutions, LLC"
REM " under sponsorship of the Defense Advanced Research Projects"
REM " Agency (DARPA)."
REM ""
REM " You can redistribute this software and/or modify it under the"
REM " terms of the Cougaar Open Source License as published on the"
REM " Cougaar Open Source Website (www.cougaar.org)."
REM ""
REM " THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS"
REM " "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT"
REM " LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR"
REM " A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT"
REM " OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,"
REM " SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT"
REM " LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,"
REM " DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY"
REM " THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT"
REM " (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE"
REM " OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE."
REM " "
REM "</copyright>"

REM ""
REM "Launch a Cougaar node"
REM ""
REM ""
REM "For usage, run with zero parameters or with --help"
REM ""

REM "Find the bootstrap jar"
if not "%CIP%" == "" goto L_1
set CIP=%COUGAAR_INSTALL_PATH%
if not "%CIP%" == "" goto L_1
echo "\$COUGAAR_INSTALL_PATH not set"
exit 1
:L_1
if exist %CIP%\lib\bootstrap.jar goto L_2
echo "Unable to find boostrap.jar in %%CIP%%: %CIP%"
:L_2

REM "Get the java command line based on our arguments"

REM "DOS lacks a simple 'exec `java ..`', so we're forced to"
REM "create a temporary .BAT file, run it, then delete it"

REM "Create a unique temporary file."
REM "Note that DOS lacks both a random file name utility *and* an"
REM "atomic file create-if-not-exists!  A timestamp-based file"
REM "name would fail in exactly the race conditions we care about."
REM "So, we iterate over potental file names and use a same-named"
REM "directory as a lock."
set base=%TEMP%
if "%base%" == "" set base=.
REM "Try cougaar_exec000.bat, cougaar_exec001.bat, ..."
set base=%base%\cougaar_exec
set x=0
set y=0
set z=0
:loop_top
set iter=%x%%y%%z%
if exist %base%%iter%.bat goto loop_next
mkdir %base%%iter% > NUL
if %ERRORLEVEL% == 0 goto loop_end
:loop_next

REM "Increment xyz.  DOS lacks math operators, so this is ugly."
REM "Note that Windows 2K and XP support SET/A foo=%foo% + 1"
REM "However, we must support all versions of Windows."
set i=0
set q=%z%
:inc
if %q% == 9 goto carry%i%
if %q% == 8 set q=9
if %q% == 7 set q=8
if %q% == 6 set q=7
if %q% == 5 set q=6
if %q% == 4 set q=5
if %q% == 3 set q=4
if %q% == 2 set q=3
if %q% == 1 set q=2
if %q% == 0 set q=1
if %i% == 2 set x=%q%
if %i% == 1 set y=%q%
if %i% == 0 set z=%q%
goto loop_top
:carry0
set z=0
set q=%y%
set i=1
goto inc
:carry1
set y=0
set q=%x%
set i=2
goto inc
:carry2
echo "Cougaar script failed! Tried temp files %base%[000...999].bat"
goto end

REM "Found our temp file name"
:loop_end
set exec=%base%%iter%.bat
echo @echo off > %exec%
rmdir %base%%iter%

echo @REM Generated by Cougaar.bat! >> %exec%

java -classpath %CIP%\lib\bootstrap.jar org.cougaar.bootstrap.CommandLine -w %* >> %exec%
if not %ERRORLEVEL% == 0 goto cleanup

call %exec%

REM "If we get here, the above java processes was cleanly stopped."
REM "But, if the batch script was killed, we won't get here, which"
REM "means we won't clean up %TEMP%, but that should be fine."
:cleanup
del %exec%
:end
